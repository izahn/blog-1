---
title: Marginal effects in models with fixed effects
author: Xiang Ao
date: '2019-01-25'
slug: marginal-effects-in-models-with-fixed-effects
categories:
  - R
  - Stata
tags:
  - R Stata
---

# Marginal effects in a linear model


Stata's margins command has been a powerful tool for many economists.  It can calculate predicted means as well as predicted marginal effects.  However, we do need to be careful when we use it when fixed effects are included.  In a linear model, everything works out fine.  However, in a non-linear model, you may not want to use margins, since it's not calculating what you have in mind.

In a linear model with fixed effects, we can do it either by "demeaning" every variable, or include dummy variables.  They return the same results.  Fortunately, marginal effects can be calculated the same way in both models.

For example:


```{r stata-chunk, engine='stata', engine.path='/usr/local/bin/stata', cache=TRUE}
clear
sysuse auto
xtset rep78
xtreg price c.mpg##c.trunk, fe
margins , dydx(mpg)
reg price c.mpg##c.trunk i.rep78
margins , dydx(mpg)

```

All is fine.

# Marginal effects in a non-linear model

In a nonlinear model, we need to be more careful:

```{r stata-chunk2, engine='stata', engine.path='/usr/local/bin/stata', cache=TRUE}
clear
sysuse auto
xtset rep78
xtpoisson price mpg trunk, fe
margins , dydx(mpg)
margins , dydx(mpg) predict(nu0)
poisson price mpg trunk i.rep78
margins , dydx(mpg)

```

In this example, "xtpoisson, fe" and "poisson i.rep78" returns the
same results.  Fixed effect Poisson model (sometimes called
conditional fixed effect Poisson) is the same models as a Poisson
model with dummies, just like a linear model (OLS with dummies is the
same as fixed effect OLS). Poisson model and OLS are unique in this
sense that there is no "incidental paramater" problem.

We see in this example, margins commands do not return the same
marginal effects, even though the models are the same.  The reason
behind this is that in a conditional fixed effect Poisson, the fixed
effects are not estimated (they are not in the final likelihood
function that gets estimated).  Therefore, we'll have to make a
decision what values to use as the values of the fixed effects.
"margins, predict(nu0)" simply set all fixed effects to zero.  On the
other hand, margins after Poisson model with dummies does not do that.
The fixed effect in that case gets estimated.  Therefore the marginal
effects in that case make more sense.

So our advise for a conditioanl Poisson model is that we should not
use margins to calculate marginal effects afterwards; instead, we
should simply stick with the original coefficient estimates.  

The same logic applies to the conditional logit model.  Fixed effects
are not estimated in that model; simply setting them to zero does not
make too much sense.  In addition, conditional logit model is not the
same model as a logit model with dummies, since there is the
"incidental paramater" problem.  Again, we should just focus on the
coefficient estimates as the effect on the logged odds.

In other words, for fixed effect (conditional) logit model, the
situation is worse: you cannot do logit with dummies, unless you have
a deep panel.  That is, when you have, say, more than 20 observations
per group, the "incidental parameter" bias becomes negligible.  If you
stay with conditional logit model, the fixed effects are not
estimated.  Unfortunately the predicted probability depends on the
fixed effects.  Stata's margins command after clogit (or xtlogit, fe)
comes with a few options, but none is reasonable for the fixed
effects.  For example, the pu0 option is to assume all fixed effects
being 0.

In a fixed effect logit model,

$$ log(P(y=1)/(1-P(y=1))) = \alpha_i +  \beta_1 x_1 + \beta_2 x_2 + \beta_{12} x_1*x_2 $$

Here $\alpha_i$ is fixed effect for each firm.  Therefore, 

$$ P(y=1) = F(\alpha_i +  \beta_1 x_1 + \beta_2 x_2 + \beta_{12} x_1*x_2) $$

$F$ can be a normal CDF or a logit function.  Therefore, without estimating $\alpha_i$, there is no way to predict $P$ in a reasonable way (assuming $\alpha=0$ is not reasonable to me).  

However, if we stick with logged odds ($LO=log(P(y=1)/(1-P(y=1)))$),
then $LO$ is a linear function of $\alpha_i$ and other covariates.  In
that case, the marginal effects of $x_1$ or $x_2$ on $Y$ has nothing
to do with $\alpha_i$.

Therefore, we can use margins command to calcuate effects on the
logged odds, which will be "predict(xb)" option.  This is in fact, not
different from the orginal coefficients; but allow you to make linear
extrapolations.

```{r stata-chunk3, engine='stata', engine.path='/usr/local/bin/stata', cache=TRUE}
clear
webuse union
clogit union c.age##i.south not_smsa grade, group(idcode)
margins, at( age=(15 20 25 30 35 40) south=(0 1)) predict(xb)
marginsplot
```



In this example, we have a fixed effect logit on union status, with
age and south interaction, age as continuous variable.  Suppose we'd
like to see the predicted logged odds of union status for different
age and south/north, then we can still use margins to predict logged
odds.  But we cannot use margins to predict probability, since the
fixed effects are not estimated.

